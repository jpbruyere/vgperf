CMAKE_MINIMUM_REQUIRED(VERSION 3.9)

PROJECT(vgperf VERSION 0.1.0 DESCRIPTION "Vector Graphic Libraries performance tests")

INCLUDE(CheckSymbolExists)
INCLUDE(CheckIncludeFile)
INCLUDE(GNUInstallDirs)

SET(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_CURRENT_SOURCE_DIR}/cmake")
SET(CMAKE_CXX_FLAGS "-W -Wall")
SET(CMAKE_EXE_LINKER_FLAGS "-lm")

IF(NOT CMAKE_BUILD_TYPE)
  SET(CMAKE_BUILD_TYPE Debug)
ENDIF()
MESSAGE(STATUS "${CMAKE_BUILD_TYPE} build.")

IF (CMAKE_BUILD_TYPE STREQUAL "Debug")
	ADD_DEFINITIONS (-DDEBUG)
ENDIF()

OPTION(TESTS_VKVG "build vkvg tests" ON)
OPTION(TESTS_CAIRO "build cairo tests" ON)
OPTION(TESTS_SKIA "build skia tests" ON)

FILE(GLOB SOURCES src/*.c)

SET(VGPERF_LIBS "")
SET(VGPERF_INCS "")

LIST(APPEND VGPERF_INCS
	${CMAKE_CURRENT_SOURCE_DIR}/src
)

FIND_PACKAGE(GLFW3)
IF(GLFW3_FOUND)
	LIST(APPEND VGPERF_LIBS
		${GLFW3_LIBRARY}
	)
	ADD_DEFINITIONS (-DWITH_GLFW3)
ENDIF()

IF (TESTS_SKIA)
	FIND_PACKAGE(Skia REQUIRED)
	FILE (GLOB SKIA_SOURCES src/skia/*.c)
	LIST (APPEND SOURCES
		${SKIA_SOURCES}
	)
	LIST(APPEND VGPERF_INCS
		${SKIA_INCLUDE_DIRS}
	)
	LIST(APPEND VGPERF_LIBS
		${SKIA_LIBRARIES}
	)
	ADD_DEFINITIONS (-DWITH_SKIA)
ENDIF (TESTS_SKIA)

IF (TESTS_VKVG)
	set(VULKAN_SDK "$ENV{VULKAN_SDK}" CACHE STRING "LunarG Vulkan SDK path")
	if (VULKAN_SDK)
			set(ENV{VULKAN_SDK} ${VULKAN_SDK})
			SET(ENV{VK_LAYER_PATH} "${VULKAN_SDK}/etc/explicit_layer.d")
	endif ()

	MESSAGE(STATUS "VULKAN_SDK = $ENV{VULKAN_SDK}")
	MESSAGE(STATUS "VK_LAYER_PATH = $ENV{VK_LAYER_PATH}")

	add_subdirectory (external/vkh)

	IF (ENABLE_VALIDATION)
		ADD_DEFINITIONS (-DUSE_VALIDATION)
	ENDIF ()

	#be aware that system libraries have priority on SDK in the finding.
	FIND_PACKAGE(Vulkan REQUIRED)
	#FIND_PACKAGE(VKVG REQUIRED)

	INCLUDE_DIRECTORIES(
		"${CMAKE_CURRENT_SOURCE_DIR}/external/vkh/src"
		"${CMAKE_CURRENT_SOURCE_DIR}/external/vkh/src"
		"${CMAKE_CURRENT_SOURCE_DIR}/external/vkh/include"
	)

	add_subdirectory (external/vkvg)

	FILE (GLOB VKVG_SOURCES src/vkvg/*.c)

	LIST (APPEND SOURCES
		${VKVG_SOURCES}
	)


	LIST(APPEND VGPERF_INCS
		${CMAKE_CURRENT_SOURCE_DIR}/external/vkh/src
		${CMAKE_CURRENT_SOURCE_DIR}/external/vkh/include
		${CMAKE_CURRENT_SOURCE_DIR}/external/vkvg/include
		${Vulkan_INCLUDE_DIRS}
	)
	LIST(APPEND VGPERF_LIBS
		vkvg_static
		${Vulkan_LIBRARIES}
		vkh_static
	)

	ADD_DEFINITIONS (-DWITH_VKVG)

	MESSAGE(STATUS "VKVG include directory: ${VKVG_INCLUDE_DIRECTORY}")
	MESSAGE(STATUS "VKVG library:           ${VKVG_LIBRARY}")
ENDIF ()

if (TESTS_CAIRO)
	SET(CAIRO_INCLUDE_DIRECTORY "/usr/include/cairo" CACHE STRING "CAIRO include directory")
	SET(CAIRO_LIBRARY "/usr/lib/x86_64-linux-gnu/libcairo.so" CACHE STRING "CAIRO library")
	FILE (GLOB CAIRO_SOURCES src/cairo/*.c)

	LIST (APPEND SOURCES
		${CAIRO_SOURCES}
	)
	LIST(APPEND VGPERF_INCS
		${CAIRO_INCLUDE_DIRECTORY}
	)
	LIST(APPEND VGPERF_LIBS
		${CAIRO_LIBRARY}
	)

	ADD_DEFINITIONS (-DWITH_CAIRO)

	MESSAGE(STATUS "CAIRO include directory: ${CAIRO_INCLUDE_DIRECTORY}")
	MESSAGE(STATUS "CAIRO library:           ${CAIRO_LIBRARY}")

	OPTION(TESTS_CAIRO_IMAGE "build cairo image surface tests" ON)
	IF (TESTS_CAIRO_IMAGE)
		ADD_DEFINITIONS (-DWITH_CAIRO_IMAGE)
	ENDIF ()

	CHECK_INCLUDE_FILE("${CAIRO_INCLUDE_DIRECTORY}/cairo-gl.h" HAVE_CAIRO_GL_H)
	MESSAGE(STATUS "cairo-gl     = ${HAVE_CAIRO_GL_H}")

	CHECK_INCLUDE_FILE("${CAIRO_INCLUDE_DIRECTORY}/cairo-xlib.h" HAVE_CAIRO_XLIB_H)
	MESSAGE(STATUS "cairo-xlib   = ${HAVE_CAIRO_XLIB_H}")

	FIND_PACKAGE (X11)
	IF (X11_FOUND AND HAVE_CAIRO_XLIB_H)
		OPTION(TESTS_CAIRO_XLIB "build cairo XLIB tests" ON)
		IF (TESTS_CAIRO_XLIB)
			LIST(APPEND VGPERF_INCS
				${X11_INCLUDE_DIRS}
			)
			LIST(APPEND VGPERF_LIBS
				${X11_LIBRARIES}
			)
			ADD_DEFINITIONS (-DWITH_CAIRO_XLIB)
		ENDIF ()
	ENDIF ()

	CHECK_SYMBOL_EXISTS (CAIRO_HAS_XCB_SURFACE "${CAIRO_INCLUDE_DIRECTORY}/cairo.h" CAIRO_HAS_XCB)
	FIND_PACKAGE (XCB)
	IF (XCB_FOUND AND CAIRO_HAS_XCB AND X11_FOUND)
		OPTION(TESTS_CAIRO_XCB "build cairo XCB tests" ON)
		IF (TESTS_CAIRO_XCB)
			LIST(APPEND VGPERF_INCS
				${X11_INCLUDE_DIRS}
				${XCB_INCLUDE_DIRS}
			)
			LIST(APPEND VGPERF_LIBS
				${X11_LIBRARIES}
				${XCB_LIBRARIES}
			)
			ADD_DEFINITIONS (-DWITH_CAIRO_XCB)
		ENDIF ()
	ENDIF ()

	CHECK_SYMBOL_EXISTS (CAIRO_HAS_GL_SURFACE "${CAIRO_INCLUDE_DIRECTORY}/cairo-gl.h" CAIRO_HAS_GL)
	MESSAGE(STATUS "CAIRO_HAS_GL   = ${CAIRO_HAS_GL}")
	FIND_PACKAGE (OpenGL)
	IF (OPENGL_FOUND AND CAIRO_HAS_GL)
		OPTION(TESTS_CAIRO_GLX "build cairo GLX tests" ON)
		IF (TESTS_CAIRO_GLX)
			LIST(APPEND VGPERF_INCS
				${OpenGL_INCLUDE_DIRS}
			)
			LIST(APPEND VGPERF_LIBS
				${OpenGL_LIBRARY_DIRS}
			)
			ADD_DEFINITIONS (-DWITH_CAIRO_GLX)
		ENDIF ()
	ENDIF ()

	FIND_PACKAGE (OpenGLES2)
	IF (OPENGLES2_FOUND)
		OPTION(TESTS_CAIRO_GLES "build cairo OpenGL ES 2 tests" ON)
		IF (TESTS_CAIRO_GLES)
			LIST(APPEND VGPERF_INCS
				${OPENGLES2_INCLUDE_DIRS}
				${OPENGL_EGL_INCLUDE_DIRS}
			)
			LIST(APPEND VGPERF_LIBS
				${OPENGLES2_LIBRARIES}
				${OPENGL_egl_LIBRARY}
			)
			ADD_DEFINITIONS (-DWITH_CAIRO_GLES)
		ENDIF ()
	ENDIF ()
endif()




ADD_EXECUTABLE(${PROJECT_NAME} ${SOURCES})

MESSAGE(STATUS "INCLUDES   = ${VGPERF_INCS}")

TARGET_INCLUDE_DIRECTORIES(${PROJECT_NAME} PRIVATE ${VGPERF_INCS})
TARGET_LINK_LIBRARIES(${PROJECT_NAME} ${VGPERF_LIBS})

